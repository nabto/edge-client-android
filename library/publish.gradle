apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

apply from: '../versioning.gradle'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/library-release.aar")
            groupId 'com.nabto.edge.client'
            artifactId 'library'
            version buildVersionName()
            artifact sourcesJar

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ?: System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ?: System.getenv('BINTRAY_API_KEY')
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'android'
        name = 'com.nabto.edge.client'
        description = "Nabto Edge client libraries for android"
        publish = true
        publicDownloadNumbers = false
        licenses = ['MIT']
        dryRun = false
        vcsUrl = 'https://github.com/nabto/nabto5-releases'
        version {
            name = buildVersionName()
            desc = "Nabto Edge Client ${buildVersionName()}"
            released = new Date()
            vcsTag = buildVersionName()
        }
    }
}
